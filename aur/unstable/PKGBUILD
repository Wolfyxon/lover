# Maintainer: Wolfyxon <wolfyxon@gmail.com>

pkgname=lover-git
pkgver=2.2.0.r44.gd98ef8e
pkgrel=1
pkgdesc="Love2D build system and runner written in Rust"
arch=("x86_64")
url="https://github.com/Wolfyxon/lover"
license=("LGPL-2.1-only")
depends=("openssl" "xz" "bzip2" "gcc-libs" "glibc")
makedepends=("cargo" "git")
optdepends=("wine: RCEdit support")
options=(!debug !lto)
source=("$pkgname::git+$url.git")
sha256sums=("SKIP")

prepare() {
    export RUSTUP_TOOLCHAIN=stable
    
    cd $srcdir/$pkgname
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

pkgver() {
    cd "$srcdir/$pkgname"
    git describe --tags --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    
    cd $srcdir/$pkgname
    cargo build --frozen --release --all-features
}

package() {
	install -Dm755 $srcdir/$pkgname/target/release/lover $pkgdir/usr/bin/lover
}
