# Maintainer: Wolfyxon <wolfyxon@gmail.com>

pkgname=lover
pkgver=2.2.1
pkgrel=1
pkgdesc="Love2D build system and runner written in Rust"
conflicts=("lover-git")
arch=("any")
url="https://github.com/Wolfyxon/lover"
license=("LGPL-2.1-only")
depends=("openssl" "xz" "bzip2" "gcc-libs" "glibc")
makedepends=("cargo")
optdepends=("wine: RCEdit support")
options=(!debug !lto)
source=("$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=("3c785b81360580b0a59eb572f14c46389f94a6e4499b4eb31447a14253043a93")

prepare() {
    export RUSTUP_TOOLCHAIN=stable
    
    cd $srcdir/$pkgname-$pkgver
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    
    cd $srcdir/$pkgname-$pkgver
    cargo build --frozen --release --all-features
}

package() {
	install -Dm755 $srcdir/$pkgname-$pkgver/target/release/lover $pkgdir/usr/bin/lover
}
